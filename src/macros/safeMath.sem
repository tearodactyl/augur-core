# Copyright (C) 2015 Forecast Foundation OU, full GPL notice in LICENSE

#### Tests ####

macro safeToAdd($a, $b):
    $c = $a + $b
    ($c >= $a && $c >= $b)

macro safeToSubtract($a, $b):
    ($b <= $a && (($a - $b) <= $a))

macro safeToMultiply($a, $b):
    $c = $a * $b
    ($a == 0 || ($c / $a) == $b)

#### Operations ####

macro safeAdd($x, $y):
    if(!safeToAdd($x, $y)):
        ~invalid()
    $x + $y

# positive inputs, positive output
macro safeSub($x, $y):
    if(!safeToSubtract($x, $y)):
        ~invalid()
    $x - $y

macro safeMul($x, $y):
    if(!safeToMultiply($x, $y)):
        ~invalid()
    $x * $y

macro safeDiv($x, $y):
    if($y == 0):
        ~invalid()
    $x / $y

### Float [fixed point] Operations ###

macro safeFxpMul($x, $y):
    if(!safeToMultiply($x, $y)):
        ~invalid()
    $x * $y / 10 ** 18

macro safeFxpDiv($x, $y):
    if($y == 0 or !safeToMultiply($x, 10 ** 18)):
        ~invalid()
    $x * 10 ** 18 / $y

macro unfix($x):
    $x / 10 ** 18

macro fix($x):
    $x * 10 ** 18
